## 项目源码

[https://github.com/luode0320/solidity-demo](https://github.com/luode0320/solidity-demo)

## 控制流

我们将介绍`Solidity`中的控制流，然后讲如何用`Solidity`实现插入排序（`InsertionSort`
），一个看起来简单，但实际上很容易写出`bug`的程序。

`Solidity`的控制流与其他语言类似，主要包含以下几种：

## 写法

- `if-else`

  ```solidity
  function ifElseTest(uint256 _number) public pure returns(bool){
      if(_number == 0){
          return(true);
      }else{
          return(false);
      }
  }
  ```

- `for循环`

  ```solidity
  function forLoopTest() public pure returns(uint256){
      uint sum = 0;
      for(uint i = 0; i < 10; i++){
          sum += i;
      }
      return(sum);
  }
  ```

- `while循环`

  ```solidity
  function whileTest() public pure returns(uint256){
      uint sum = 0;
      uint i = 0;
      while(i < 10){
          sum += i;
          i++;
      }
      return(sum);
  }
  ```

- `do-while循环`

  ```solidity
  function doWhileTest() public pure returns(uint256){
      uint sum = 0;
      uint i = 0;
      do{
          sum += i;
          i++;
      }while(i < 10);
      return(sum);
  }
  ```

- `三元运算符`

    - 规则`条件 ? 条件为真的表达式:条件为假的表达式`。此运算符经常用作`if`语句的快捷方式。

  ```solidity
  function ternaryTest(uint256 x, uint256 y) public pure returns(uint256){
      // return the max of x and y
      return x >= y ? x: y; 
  }
  ```

**另外还有`continue`（立即进入下一个循环）和`break`（跳出当前循环）关键字可以使用。**

## 用`Solidity`实现插入排序

**写在前面：90%以上的人用`Solidity`写插入算法都会出错。**

### 插入排序

排序算法解决的问题是将无序的一组数字，例如`[2, 5, 3, 1]`，从小到大依次排列好。

插入排序（`InsertionSort`）是最简单的一种排序算法，也是很多人学习的第一个算法。

它的思路很简单，从前往后，依次将每一个数和排在他前面的数字比大小，如果比前面的数字小，就互换位置。

![插入排序](../../../picture/92b034385c440e08bc8551c97df0a2e3.gif)

### `python`代码

我们可以先看一下插入排序的python代码：

```python
def insertionSort(arr):
    # 遍历数组中的每一个元素，从第二个元素开始
    for i in range(1, len(arr)):
        # 选取当前元素作为待插入的关键值
        key = arr[i]

        # 初始化 j 为当前元素的前一个位置
        j = i - 1

        # 当 j 大于等于 0 且当前关键值小于 arr[j] 时，将 arr[j] 向右移动一位
        while j >= 0 and key < arr[j]:
            arr[j + 1] = arr[j]
            j -= 1

        # 在正确的位置插入关键值
        arr[j + 1] = key

    # 返回排序后的数组
    return arr
```

### 改写成`Solidity`后有`BUG`

一共8行`python`代码就可以完成插入排序，非常简单。那么我们将它改写成`Solidity`代码，将函数，变量，循环等等都做了相应的转换，只需要9行代码：

```solidity
    // 插入排序 错误版
    function insertionSortWrong(uint[] memory a) public pure returns(uint[] memory) {    
        for (uint i = 1;i < a.length;i++){
            uint temp = a[i];
            uint j=i-1;
            while( (j >= 0) && (temp < a[j])){
                a[j+1] = a[j];
                j--;
            }
            a[j+1] = temp;
        }
        return(a);
    }
```

### 正确的Solidity插入排序

花了几个小时，在社群一个朋友的帮助下，终于找到了`bug`所在。`Solidity`中最常用的变量类型是`uint`
，也就是正整数，取到负值的话，会报`underflow`错误。而在插入算法中，变量`j`有可能会取到`-1`，引起报错。

这里，我们需要把`j`加1，让它无法取到负值。正确代码：

```solidity
	// 插入排序 正确版
    function insertionSort(uint[] memory a) public pure returns(uint[] memory) {
        // note that uint can not take negative value
        for (uint i = 1;i < a.length;i++){
            uint temp = a[i];
            uint j=i;
            while( (j >= 1) && (temp < a[j-1])){
                a[j] = a[j-1];
                j--;
            }
            a[j] = temp;
        }
        return(a);
    }
```

## 完整代码

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.21;

contract InsertionSort {
    // if else
    function ifElseTest(uint256 _number) public pure returns (bool) {
        if (_number == 0) {
            return (true);
        } else {
            return (false);
        }
    }

    // for loop
    function forLoopTest() public pure returns (uint256) {
        uint sum = 0;
        for (uint i = 0; i < 10; i++) {
            sum += i;
        }
        return (sum);
    }

    // while
    function whileTest() public pure returns (uint256) {
        uint sum = 0;
        uint i = 0;
        while (i < 10) {
            sum += i;
            i++;
        }
        return (sum);
    }

    // do-while
    function doWhileTest() public pure returns (uint256) {
        uint sum = 0;
        uint i = 0;
        do {
            sum += i;
            i++;
        } while (i < 10);
        return (sum);
    }

    // 三元运算符 ternary/conditional operator
    function ternaryTest(uint256 x, uint256 y) public pure returns (uint256) {
        // return the max of x and y
        return x >= y ? x : y;
    }

    // 插入排序 错误版
    function insertionSortWrong(
        uint[] memory a
    ) public pure returns (uint[] memory) {
        for (uint i = 1; i < a.length; i++) {
            uint temp = a[i];
            uint j = i - 1;
            while ((j >= 0) && (temp < a[j])) {
                a[j + 1] = a[j];
                j--;
            }
            a[j + 1] = temp;
        }
        return (a);
    }

    // 插入排序 正确版
    function insertionSort(
        uint[] memory a
    ) public pure returns (uint[] memory) {
        // note that uint can not take negative value
        for (uint i = 1; i < a.length; i++) {
            uint temp = a[i];
            uint j = i;
            while ((j >= 1) && (temp < a[j - 1])) {
                a[j] = a[j - 1];
                j--;
            }
            a[j] = temp;
        }
        return (a);
    }
}

```

## 部署调试合约

修改 `.env` 中

```
# 部署的合约名称
DEPLOY_CONTRACT_NAME=InsertionSort
```

编写调试逻辑 `scripts\deploy.ts`:

```ts
import { ethers } from "hardhat";
import dotenv from "dotenv";

// 加载环境变量
dotenv.config();

// 需要部署的合约名称
const contractName: string = process.env.DEPLOY_CONTRACT_NAME!;

// 调用合约方法
async function exec(contract: any) {
    console.log("ifElseTest(0):", await contract.ifElseTest(0));
    console.log("ifElseTest(1):", await contract.ifElseTest(1));

    console.log("forLoopTest():", await contract.forLoopTest());

    console.log("whileTest():", await contract.whileTest());

    console.log("doWhileTest():", await contract.doWhileTest());

    console.log("ternaryTest(5, 10):", await contract.ternaryTest(5, 10));
    console.log("ternaryTest(10, 5):", await contract.ternaryTest(10, 5));

    const arrayToSort = [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5];
    console.log("insertionSort [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5]:", await contract.insertionSort(arrayToSort));
}

// 定义一个异步函数 main，用于部署合约。
async function main() {
    console.log("_________________________启动部署________________________________");
    const [deployer] = await ethers.getSigners();
    console.log("部署地址:", deployer.address);

    // 获取账户的余额
    const balance = await deployer.provider.getBalance(deployer.address);
    // 将余额转换为以太币 (ETH)
    console.log("账户余额 balance(wei):", balance.toString());
    const balanceInEther = ethers.formatEther(balance);
    console.log("账户余额 balance(eth):", balanceInEther);

    console.log("_________________________部署合约________________________________");
    // 获取合约工厂。
    const contractFactory = await ethers.getContractFactory(contractName);
    // 部署合约
    const contract = await contractFactory.deploy();
    //  等待部署完成
    await contract.waitForDeployment()
    console.log(`合约地址: ${contract.target}`);

    console.log("_________________________合约调用________________________________");
    await exec(contract);
}

// 执行 main 函数，并处理可能发生的错误。
main()
    .then(() => process.exit(0)) // 如果部署成功，则退出进程。
    .catch(error => {
        console.error(error); // 如果发生错误，则输出错误信息。
        process.exit(1); // 退出进程，并返回错误代码 1。
    });
```

运行结果:

```sh
$ yarn hardhat run scripts/deploy.ts 
yarn run v1.22.22
$ E:\solidity-demo\10.控制流\node_modules\.bin\hardhat run scripts/deploy.ts
_________________________启动部署________________________________
部署地址: 0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266
账户余额 balance(wei): 10000000000000000000000
账户余额 balance(eth): 10000.0
_________________________部署合约________________________________
合约地址: 0x5FbDB2315678afecb367f032d93F642f64180aa3
_________________________合约调用________________________________
ifElseTest(0): true
ifElseTest(1): false
forLoopTest(): 45n
whileTest(): 45n
doWhileTest(): 45n
ternaryTest(5, 10): 10n
ternaryTest(10, 5): 10n
insertionSort [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5]: Result(11) [
  1n, 1n, 2n, 3n, 3n,
  4n, 5n, 5n, 5n, 6n,
  9n
]
Done in 2.52s.
```

## 总结

这一讲，我们介绍了`Solidity`中控制流，并且用`Solidity`写了插入排序。

看起来很简单，但实际很难。这就是`Solidity`，坑很多，每个月都有项目因为这些小`bug`损失几千万甚至上亿美元。

掌握好基础，不断练习，才能写出更好的`Solidity`代码。