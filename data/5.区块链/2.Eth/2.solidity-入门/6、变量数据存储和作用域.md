## 项目源码

[https://github.com/luode0320/solidity-demo](https://github.com/luode0320/solidity-demo)

## Solidity中的引用类型

**引用类型(Reference Type)**：包括数组（`array`）和结构体（`struct`），由于这类变量比较复杂，占用存储空间大，我们在使用时必须要声明数据存储的位置。

## 数据位置

Solidity 数据存储位置有三类：`storage`，`memory`和`calldata`。不同存储位置的`gas`成本不同。

- `storage`类型的数据存在链上，类似计算机的硬盘，消耗`gas`多；
- `memory`和`calldata`类型的临时存在内存里，消耗`gas`少。

## 用法

1. `storage`：合约里的状态变量默认都是`storage`，存储在链上。

2. `memory`：函数里的参数和临时变量一般用`memory`，存储在内存中，不上链。尤其是**如果返回数据类型是变长**的情况下，必须加memory修饰

    - 例如：**string, bytes, array和自定义结构**

3. `calldata`：和`memory`类似，存储在内存中，不上链。与`memory`的不同点在于`calldata`变量不能修改（`immutable`），一般用于函数的参数。

```solidity
function fCalldata(uint[] calldata _x) public pure returns(uint[] calldata){
    //参数为calldata数组，不能被修改
    // _x[0] = 0 //这样修改会报错
    return(_x);
}
```

## 数据位置和赋值规则

在不同存储类型相互赋值时候，有时会产生独立的副本（**修改新变量不会影响原变量**），有时会产生引用（**修改新变量会影响原变量
**）。

规则如下：

- 赋值本质上是创建**引用**指向本体，因此修改本体或者是引用，变化可以被同步：

    - `storage`（合约的状态变量）赋值给本地`storage`（函数里的）时候，会创建引用，改变新变量会影响原变量。

  ```solidity
  uint[] x = [1,2,3]; // 状态变量：数组 x
  
  function fStorage() public{
      //声明一个storage的变量 xStorage，指向x。修改xStorage也会影响x
      uint[] storage xStorage = x;
      xStorage[0] = 100;
  }
  ```
    - `memory`赋值给`memory`，会创建引用，改变新变量会影响原变量。

- 其他情况下，赋值创建的是本体的副本，即对二者之一的修改，并不会同步到另一方

## 完整代码

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.21;

contract DataStorage {
    // x的数据位置是存储。
    // 这是唯一一个
    // 可以省略数据位置。
    uint[] private x = [1, 2, 3];

    // 添加一个视图函数来获取x的值
    function getX() public view returns (uint[] memory) {
        return x;
    }

    // 声明一个storage的变量xStorage，指向x。修改xStorage也会影响x
    function fStorage() public {
        uint[] storage xStorage = x;
        xStorage[0] = 100;
    }

    // 声明一个Memory的变量xMemory，复制x。修改xMemory不会影响x
    function fMemory() public view {
        uint[] memory xMemory = x;
        xMemory[0] = 100;
        xMemory[1] = 200;

        uint[] memory xMemory2 = x;
        xMemory2[0] = 300;
    }

    // 参数为calldata数组,不能被修改
    function fCalldata(
        uint[] calldata _x
    ) public pure returns (uint[] calldata) {
        //  _x[0] = 0 // 这样修改会报错
        return (_x);
    }
}
```

## 部署调试合约

修改 `.env` 中

```
# 部署的合约名称
DEPLOY_CONTRACT_NAME=DataStorage
```

编写调试逻辑 `scripts\deploy.ts`:

```ts
import { ethers } from "hardhat";
import dotenv from "dotenv";

// 加载环境变量
dotenv.config();

// 需要部署的合约名称
const contractName: string = process.env.DEPLOY_CONTRACT_NAME!;

// 调用合约方法
async function exec(contract: any) {
    console.log("查询 x : ", await contract.getX());

    console.log("调用 fStorage() 方法, 将 x[0] 赋值为 100:");
    await contract.fStorage();
    console.log("fStorage() 方法调用完成: ", await contract.getX());

    console.log("调用 fMemory() 方法,修改xMemory不会影响x:");
    await contract.fMemory();
    console.log("fMemory() 方法调用完成: ", await contract.getX());

    // 调用 fCalldata() 方法
    console.log("调用 fCalldata() 方法,参数为calldata数组,不能被修改:");
    const calldataInput = [10, 20, 30];
    const result = await contract.fCalldata(calldataInput);
    console.log("fCalldata() 方法返回结果:", result);
}

// 定义一个异步函数 main，用于部署合约。
async function main() {
    console.log("_________________________启动部署________________________________");
    const [deployer] = await ethers.getSigners();
    console.log("部署地址:", deployer.address);

    // 获取账户的余额
    const balance = await deployer.provider.getBalance(deployer.address);
    // 将余额转换为以太币 (ETH)
    console.log("账户余额 balance(wei):", balance.toString());
    const balanceInEther = ethers.formatEther(balance);
    console.log("账户余额 balance(eth):", balanceInEther);

    console.log("_________________________部署合约________________________________");
    // 获取合约工厂。
    const contractFactory = await ethers.getContractFactory(contractName);
    // 部署合约
    const contract = await contractFactory.deploy();
    //  等待部署完成
    await contract.waitForDeployment()
    console.log(`合约地址: ${contract.target}`);

    console.log("_________________________合约调用________________________________");
    await exec(contract);
}

// 执行 main 函数，并处理可能发生的错误。
main()
    .then(() => process.exit(0)) // 如果部署成功，则退出进程。
    .catch(error => {
        console.error(error); // 如果发生错误，则输出错误信息。
        process.exit(1); // 退出进程，并返回错误代码 1。
    });
```

运行结果:

```sh
$ yarn hardhat run scripts/deploy.ts 
yarn run v1.22.22
$ E:\solidity-demo\5.变量数据存储和作用域\node_modules\.bin\hardhat run scripts/deploy.ts
Compiled 1 Solidity file successfully (evm target: paris).
_________________________启动部署________________________________
部署地址: 0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266
账户余额 balance(wei): 10000000000000000000000
账户余额 balance(eth): 10000.0
_________________________部署合约________________________________
合约地址: 0x5FbDB2315678afecb367f032d93F642f64180aa3
_________________________合约调用________________________________
查询 x :  Result(3) [ 1n, 2n, 3n ]
调用 fStorage() 方法, 将 x[0] 赋值为 100:
fStorage() 方法调用完成:  Result(3) [ 100n, 2n, 3n ]
调用 fMemory() 方法,修改xMemory不会影响x:
fMemory() 方法调用完成:  Result(3) [ 100n, 2n, 3n ]
调用 fCalldata() 方法,参数为calldata数组,不能被修改:
fCalldata() 方法返回结果: Result(3) [ 10n, 20n, 30n ]
Done in 2.86s.
```

## 变量的作用域

`Solidity`中变量按作用域划分有三种，分别是:

- 状态变量: 在其他语言中, 我们一般把这个位置的当作全局变量。
- 局部变量: 代码块内部的变量。
- 全局变量: Solidity 语言层面的（原生） API, 比如当前区块的block、消息msg等在编写代码时是默认隐藏的, 这些是solidity的全局变量。

### 1. 状态变量

状态变量是数据存储在链上的变量，所有合约内函数都可以访问，`gas`消耗高。状态变量在合约内、函数外声明：

```solidity
contract Variables {
    uint public x = 1;
    uint public y;
    string public z;
}
```

我们可以在函数里更改状态变量的值：

```solidity
function foo() external{
    // 可以在函数里更改状态变量的值
    x = 5;
    y = 2;
    z = "0xAA";
}
```

### 2. 局部变量

局部变量是仅在函数执行过程中有效的变量，函数退出后，变量无效。局部变量的数据存储在内存里，不上链，`gas`低。局部变量在函数内声明：

```solidity
function bar() external pure returns(uint){
    uint xx = 1;
    uint yy = 3;
    uint zz = xx + yy;
    return(zz);
}
```

### 3. 全局变量

全局变量是全局范围工作的变量，都是`solidity`预留关键字。他们可以在函数内不声明直接使用：

```solidity
function global() external view returns(address, uint, bytes memory){
    address sender = msg.sender;
    uint blockNum = block.number;
    bytes memory data = msg.data;
    return(sender, blockNum, data);
}
```

在上面例子里，我们使用了3个常用的全局变量：

- `msg.sender`: 消息发送者 (当前 caller)
- `block.number`: 当前区块的number
- `msg.data`: 完整call data完整call data

他们分别代表请求发起地址，当前区块高度，和请求数据。

**我们下面对全局变量一些说明, 因为全局变量一般是隐藏的,
我们使用的话需要专门[查看文档](https://learnblockchain.cn/docs/solidity/units-and-global-variables.html#)!!!**

## 全局变量-以太单位

`Solidity`中不存在小数点，以`0`代替为小数点，来确保交易的精确度，并且防止精度的损失，利用以太单位可以避免误算的问题，方便程序员在合约中处理货币交易。

- `wei`: 1
- `gwei`: 1e9 = 1000000000
- `ether`: 1e18 = 1000000000000000000

```solidity
function weiUnit() external pure returns(uint) {
    assert(1 wei == 1e0);
    assert(1 wei == 1);
    return 1 wei;
}

function gweiUnit() external pure returns(uint) {
    assert(1 gwei == 1e9);
    assert(1 gwei == 1000000000);
    return 1 gwei;
}

function etherUnit() external pure returns(uint) {
    assert(1 ether == 1e18);
    assert(1 ether == 1000000000000000000);
    return 1 ether;
}
```

## 全局变量-时间单位

可以在合约中规定一个操作必须在一周内完成，或者某个事件在一个月后发生。这样就能让合约的执行可以更加精确，不会因为技术上的误差而影响合约的结果。

因此，时间单位在`Solidity`中是一个重要的概念，有助于提高合约的可读性和可维护性。

- `seconds`: 1
- `minutes`: 60 seconds = 60
- `hours`: 60 minutes = 3600
- `days`: 24 hours = 86400
- `weeks`: 7 days = 604800

```solidity
function secondsUnit() external pure returns(uint) {
    assert(1 seconds == 1);
    return 1 seconds;
}

function minutesUnit() external pure returns(uint) {
    assert(1 minutes == 60);
    assert(1 minutes == 60 seconds);
    return 1 minutes;
}

function hoursUnit() external pure returns(uint) {
    assert(1 hours == 3600);
    assert(1 hours == 60 minutes);
    return 1 hours;
}

function daysUnit() external pure returns(uint) {
    assert(1 days == 86400);
    assert(1 days == 24 hours);
    return 1 days;
}

function weeksUnit() external pure returns(uint) {
    assert(1 weeks == 604800);
    assert(1 weeks == 7 days);
    return 1 weeks;
}
```

## 全局变量-特殊变量和函数

> 为了方便理解，可以把这些变量和函数理解为 Solidity 语言层面的（原生） API 。

在全局命名空间中已经存在了（预设了）一些特殊的变量和函数，他们主要用来提供关于区块链的信息或一些通用的工具函数。

## 全局变量-区块和交易属性

- `blockhash(uint blockNumber) returns (bytes32)`：指定区块的区块哈希 —— 仅可用于最新的 256 个区块且不包括当前区块，否则返回
  0 。
- `block.basefee` (`uint`): 当前区块的基础费用，参考： ([EIP-3198](https://eips.ethereum.org/EIPS/eip-3198)
  和 [EIP-1559](https://eips.ethereum.org/EIPS/eip-1559))
- `block.chainid` (`uint`): 当前链 id
- `block.coinbase` ( `address` ): 挖出当前区块的矿工地址
- `block.difficulty` ( `uint` ): 当前区块难度
- `block.gaslimit` ( `uint` ): 当前区块 gas 限额
- `block.number` ( `uint` ): 当前区块号
- `block.timestamp` ( `uint`): 自 unix epoch 起始当前区块以秒计的时间戳
- `gasleft() returns (uint256)` ：剩余的 gas
- `msg.data` ( `bytes` ): 完整的 calldata
- `msg.sender` ( `address` ): 消息发送者（当前调用）
- `msg.sig` ( `bytes4` ): calldata 的前 4 字节（也就是函数标识符）
- `msg.value` ( `uint` ): 随消息发送的 wei 的数量
- `tx.gasprice` (`uint`): 交易的 gas 价格
- `tx.origin` ( `address` ): 交易发起者（完全的调用链）

注意:

- 不要依赖 `block.timestamp` 和 `blockhash` 产生随机数，除非你明确知道自己做的用意。

- 时间戳和区块哈希在一定程度上都可能受到挖矿矿工影响。例如，挖矿社区中的恶意矿工可以用某个给定的哈希来运行赌场合约的
  payout 函数，而如果他们没收到钱，还可以用一个不同的哈希重新尝试。

- 当前区块的时间戳必须严格大于最后一个区块的时间戳，但这里能确保也需要它是在权威链上的两个连续区块。
- 基于可扩展因素，区块哈希不是对所有区块都有效。你仅仅可以访问最近 256 个区块的哈希，其余的哈希均为零。
- `blockhash` 函数之前是使用 `block.blockhash`， `block.blockhash` 在 0.4.22 开始不推荐使用，在 0.5.0 已经移除了。
- `gasleft` 函数之前是使用 `msg.gas`, `msg.gas` 在 0.4.21 开始不推荐使用，在 0.5.0 已经移除了。
- 在 0.7.0, `now` ( `block.timestamp` 的别名) 被移除了。

## 全局变量-bytes 成员函数

- `bytes.concat(...) returns (bytes memory)`: 将可变字节数和bytes1，…，bytes32参数连接到一个字节数组中

## 全局变量-string 成员函数

- `string.concat(...) returns (string memory)`: 将可变数量的字符串参数连接到一个字符串数组

## 全局变量-错误处理

可以参阅专门的章节 [assert and require](https://learnblockchain.cn/docs/solidity/control-structures.html#assert-and-require)
参阅有关错误处理以及何时使用哪个函数的更多详细信息。

- 如果不满足条件，则会导致Panic 错误，则撤销状态更改 - 用于检查内部错误。

  ```solidity
  assert(bool condition)
  ```

- 如果条件不满足则撤销状态更改 - 用于检查由输入或者外部组件引起的错误。

  ```solidity
  require(bool condition)
  ```

- 如果条件不满足则撤销状态更改 - 用于检查由输入或者外部组件引起的错误，可以同时提供一个错误消息。

  ```solidity
  require(bool condition, string memory message)
  ```

- 终止运行并撤销状态更改。

  ```solidity
  revert()
  ```

- 终止运行并撤销状态更改，可以同时提供一个解释性的字符串。

  ```solidity
  revert(string memory reason)
  ```

## 全局变量-数学和密码学函数

- 计算 `(x + y) % k`，加法会在任意精度下执行，并且加法的结果即使超过 `2**256` 也不会被截取。

    - 从 0.5.0 版本的编译器开始会加入对 `k != 0` 的校验（assert）。

  ```solidity
  addmod(uint x, uint y, uint k) returns (uint)
  ```

- 计算 `(x * y) % k`，乘法会在任意精度下执行，并且乘法的结果即使超过 `2**256` 也不会被截取。

    - 从 0.5.0 版本的编译器开始会加入对 `k != 0` 的校验（assert）。

  ```solidity
  mulmod(uint x, uint y, uint k) returns (uint)
  ```

- 计算 Keccak-256 哈希。之前 `keccak256` 的别名函数 `sha3` 在0.5.0中已经移除。

  ```solidity
  keccak256((bytes memory) returns (bytes32)
  ```

- 计算参数的 SHA-256 哈希。

  ```solidity
  sha256(bytes memory) returns (bytes32)
  ```

- 计算参数的 RIPEMD-160 哈希。

  ```solidity
  ripemd160(bytes memory) returns (bytes20)
  ```

- 利用椭圆曲线签名恢复与公钥相关的地址，错误返回零值。

    - 函数参数对应于 ECDSA签名的值:
        - `r` = 签名的前 32 字节
        - `s` = 签名的第2个32 字节
        - `v` = 签名的最后一个字节
    - 函数返回一个 `address`, 而不是可交易 `address payable` 。

  ```solidity
  ecrecover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) returns (address)
  ```

## 全局变量-地址成员

- 以 Wei 为单位的地址类型 Address 的余额

    - 在版本0.5.0之前，Solidity允许通过合约实例来访问地址的成员，例如 `this.balance`
    - 不过现在禁止这样做，必须显式转换为地址后访问，如： `address（this）.balance` 。

  ```solidity
  <address>.balance (uint256)
  ```

- 在地址类型 Address 上的代码(可以为空)

  ```solidity
  <address>.code (bytes memory)
  ```

- 地址类型 Address 的codehash
  ```solidity
  <address>.codehash (bytes32)
  ```

- 向地址类型 Address 发送数量为 amount 的 Wei，失败时抛出异常，使用固定（不可调节）的 2300 gas 的矿工费。

  ```solidity
  <address payable>.transfer(uint256 amount)
  ```

- 向地址类型 Address 发送数量为 amount 的 Wei，失败时返回 `false`，发送 2300 gas 的矿工费用，不可调节。

    - 使用 `send` 有很多危险：
    - 如果调用栈深度已经达到 1024（这总是可以由调用者所强制指定），转账会失败；
    - 并且如果接收者用光了 gas，转账同样会失败。
    - 为了保证以太币转账安全，总是检查 `send` 的返回值，利用 `transfer` 方式取回钱会更好。

  ```solidity
  <address payable>.send(uint256 amount) returns (bool)
  ```

- 用给定的有效载荷（payload）发出低级 `CALL` 调用，返回成功状态及返回数据，发送所有可用 gas，也可以调节 gas。

    - 在执行另一个合约函数时，应该尽可能避免使用 `.call()` ，因为它绕过了类型检查，函数存在检查和参数打包。

  ```solidity
  <address>.call(bytes memory) returns (bool, bytes memory)
  ```

- 用给定的有效载荷 发出低级 `DELEGATECALL` 调用 ，返回成功状态并返回数据，发送所有可用 gas，也可以调节 gas。

  ```solidity
  <address>.delegatecall(bytes memory) returns (bool, bytes memory)
  ```

- 用给定的有效载荷 发出低级 `STATICCALL` 调用 ，返回成功状态并返回数据，发送所有可用 gas，也可以调节 gas。

  ```solidity
  <address>.staticcall(bytes memory) returns (bool, bytes memory)
  ```

更多信息，参考 [地址类型 Address](https://learnblockchain.cn/docs/solidity/types.html#address) 部分。

## 全局变量-合约相关

- 当前合约，可以显示转换为地址类型 Address
  ```solidity
  this
  ```

- 销毁合约，并把余额发送到指定地址类型 Address 。

    - 请注意， `selfdestruct` 具有从EVM继承的一些特性：
    - 接收合约的 receive 函数 不会执行。
    - 合约仅在交易结束时才真正被销毁，并且 `revert` 可能会“撤消”销毁。

  ```solidity
  selfdestruct(address payable recipient)
  ```

## 总结

在这一讲，我们介绍了`Solidity`中的引用类型，数据位置和变量的作用域。

重点是`storage`, `memory`和`calldata`三个关键字的用法。他们出现的原因是为了节省链上有限的存储空间和降低`gas`。